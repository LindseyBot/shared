plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'maven-publish'
}

group = 'net.notfab.lindsey'
version = '0.1.13'
sourceCompatibility = '15'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveBaseName.set(project.name)
    manifest {
        attributes 'Implementation-Version': project.version
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/LindseyBot/shared"
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
            groupId = group
            artifactId = rootProject.name
            version = version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compileOnly 'net.jodah:expiringmap:0.5.8'
    compileOnly 'com.squareup.okhttp3:okhttp:3.14.9'
    compileOnly 'org.json:json:20190722'
    compileOnly 'org.springframework.boot:spring-boot-starter-data-jpa'
    compileOnly 'org.springframework.boot:spring-boot-starter-data-mongodb'
    compileOnly 'org.springframework.boot:spring-boot-starter-amqp'
    compileOnly 'org.springframework.boot:spring-boot-starter'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}
